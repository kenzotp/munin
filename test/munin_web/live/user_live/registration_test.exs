defmodule MuninWeb.UserLive.RegistrationTest do
  # Toggles the :registration_open application env below, so this module
  # cannot run concurrently with other tests touching the same key.
  use MuninWeb.ConnCase, async: false

  import Phoenix.LiveViewTest
  import Munin.AccountsFixtures

  # Registration is closed by default (see MuninWeb.UserAuth.registration_open?/0).
  # These tests exercise the open case; restore whatever was configured before.
  setup do
    previous = Application.get_env(:munin, :registration_open)
    Application.put_env(:munin, :registration_open, true)
    on_exit(fn -> Application.put_env(:munin, :registration_open, previous) end)
    :ok
  end

  describe "Registration page" do
    test "renders registration page", %{conn: conn} do
      {:ok, _lv, html} = live(conn, ~p"/users/register")

      assert html =~ "Register"
      assert html =~ "Log in"
    end

    test "redirects if already logged in", %{conn: conn} do
      result =
        conn
        |> log_in_user(user_fixture())
        |> live(~p"/users/register")
        |> follow_redirect(conn, ~p"/")

      assert {:ok, _conn} = result
    end

    test "renders errors for invalid data", %{conn: conn} do
      {:ok, lv, _html} = live(conn, ~p"/users/register")

      result =
        lv
        |> element("#registration_form")
        |> render_change(user: %{"email" => "with spaces"})

      assert result =~ "Register"
      assert result =~ "must have the @ sign and no spaces"
    end
  end

  describe "register user" do
    test "creates account but does not log in", %{conn: conn} do
      {:ok, lv, _html} = live(conn, ~p"/users/register")

      email = unique_user_email()
      form = form(lv, "#registration_form", user: valid_user_attributes(email: email))

      {:ok, _lv, html} =
        render_submit(form)
        |> follow_redirect(conn, ~p"/users/log-in")

      assert html =~
               ~r/An email was sent to .*, please access it to confirm your account/
    end

    test "renders errors for duplicated email", %{conn: conn} do
      {:ok, lv, _html} = live(conn, ~p"/users/register")

      user = user_fixture(%{email: "test@email.com"})

      result =
        lv
        |> form("#registration_form",
          user: %{"email" => user.email}
        )
        |> render_submit()

      assert result =~ "has already been taken"
    end
  end

  describe "registration navigation" do
    test "redirects to login page when the Log in button is clicked", %{conn: conn} do
      {:ok, lv, _html} = live(conn, ~p"/users/register")

      {:ok, _login_live, login_html} =
        lv
        |> element("main a", "Log in")
        |> render_click()
        |> follow_redirect(conn, ~p"/users/log-in")

      assert login_html =~ "Log in"
    end
  end

  describe "registration closed" do
    setup do
      Application.put_env(:munin, :registration_open, false)
      :ok
    end

    test "redirects to the login page with a flash", %{conn: conn} do
      {:ok, redirected_conn} =
        conn
        |> live(~p"/users/register")
        |> follow_redirect(conn, ~p"/users/log-in")

      assert Phoenix.Flash.get(redirected_conn.assigns.flash, :error) ==
               "Registration is closed."
    end
  end
end
